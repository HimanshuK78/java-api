apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java-library-distribution'
apply plugin: 'maven-publish'
apply plugin: 'bintray'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.elastic'
version = '0.0.2-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.6
targetCompatibility = 1.6

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.gson:gson:2.2.4"
    testRuntime "org.codehaus.groovy:groovy-all:2.0.5"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}


uploadArchives {
    repositories {
        mavenLocal()
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                packaging 'jar'
                description 'Java API for elastic.io'
                url 'http://www.elastic.io'

                scm {
                    url 'scm:git@github.com:elasticio/java-api.git'
                    connection 'scm:git@github.com:elasticio/java-api.git'
                    developerConnection 'scm:git@github.com:elasticio/java-api.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray {
        user = bintrayUser //this usually comes form gradle.properties file in ~/.gradle
        key = bintrayKey //this usually comes form gradle.properties file in ~/.gradle
        publications = ['mavenJava'] // see publications closure
        pkg { //package will be created if does not exist
            repo = 'maven'
//            userOrg = 'myorg' // an optional organization name when the repo belongs to one of the user's orgs
            name = 'java-api'
            licenses = ['Apache-2.0']
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
